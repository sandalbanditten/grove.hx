(require "helix/misc.scm")
(require (prefix-in helix. "helix/commands.scm"))
(require (prefix-in layout. "../domain/layout.scm"))
(require (prefix-in model. "../domain/model.scm"))
(require (prefix-in path. "../domain/path.scm"))
(require (prefix-in palette. "../domain/palette.scm"))
(require (prefix-in render. "helix/render.scm"))
(require (prefix-in scanner. "scanner.scm"))
(require (prefix-in git. "git.scm"))
(require (prefix-in host. "helix/host.scm"))
(require (prefix-in files. "helix/files.scm"))
(require (prefix-in hooks. "helix/hooks.scm"))
(require (prefix-in input. "helix/input.scm"))
(require (prefix-in theme. "helix/theme.scm"))
(require (prefix-in component. "helix/component.scm"))

(provide start! focus! visibility-toggle!)

(define REFRESH-INTERVAL-MS 2000)

(define *model* #f)
(define *latest-frame* #f)
(define *focus-next-frame?* #f)
(define *reveal-next-frame?* #f)
(define *started?* #f)
(define *theme-sources* '())

(struct rendered-frame (root layout))

(define (observe model-at-observation focus?)
  (define observed-root (host.workspace-root))
  (define active-path (host.active-path))
  (define active-id
    (path.id-for-path observed-root active-path))
  (define scan-scope
    (model.plan-file-tree-scan
      model-at-observation
      observed-root
      active-id
      focus?))
  (model.observation-snapshot
    observed-root
    (scanner.scan observed-root scan-scope)
    (git.observe observed-root)
    active-id))

(define (refresh-now!)
  (define snapshot (observe *model* #f))
  (dispatch! model.observation-received snapshot))

; Startup scans through the Active file's ancestors so the first tree already
; holds the path Helix is showing.
(define (reveal-now!)
  (define snapshot (observe *model* #t))
  (dispatch! model.first-observation-received snapshot))

(define (schedule-refresh!)
  (enqueue-thread-local-callback refresh-now!))

(define (created-file! root id)
  (dispatch! model.created-file-open-requested root id))

; ADR 0011: keep timer recursion at module scope to avoid an expired capture.
(define (refresh-and-reschedule!)
  (refresh-now!)
  (subscribe-to-refresh!))

(define (subscribe-to-refresh!)
  (enqueue-thread-local-callback-with-delay
    REFRESH-INTERVAL-MS
    refresh-and-reschedule!))

(define (execute-command! command)
  (define kind (model.model-command-kind command))
  (define arguments (model.model-command-arguments command))
  (cond
    [(equal? kind 'refresh)
      (schedule-refresh!)]
    [(equal? kind 'open-file)
      (apply
        (lambda (root id mode)
          (host.open-file! (path.path-for-id root id) mode))
        arguments)]
    [(equal? kind 'create)
      (apply
        files.prompt-create!
        (append arguments (list created-file! refresh-now!)))]
    [(equal? kind 'rename)
      (apply files.prompt-rename! (append arguments (list refresh-now!)))]
    [(equal? kind 'delete)
      (apply files.confirm-delete! (append arguments (list refresh-now!)))]
    [else (error "unknown Model command")]))

(define (release-pane!)
  (component.apply-clip! 0)
  (set! *latest-frame* #f)
  (input.cancel!))

(define (commit! update-result)
  (define was-requested? (model.presentation-requested? *model*))
  (set! *model* (model.update-result-model update-result))
  (define requested? (model.presentation-requested? *model*))
  (define presentation-changed?
    (not (equal? was-requested? requested?)))
  (when (and presentation-changed? (not requested?))
    (release-pane!))
  (define command (model.update-result-command update-result))
  (when command
    (execute-command! command))
  presentation-changed?)

(define (commit-and-redraw-if-needed! update-result)
  (when (commit! update-result)
    (helix.redraw)))

(define (dispatch! transition . arguments)
  (commit-and-redraw-if-needed!
    (apply transition *model* arguments)))

(define (render-current! geometry frame)
  (cond
    [*focus-next-frame?*
      (define snapshot (observe *model* #t))
      (set! *focus-next-frame?* #f)
      (set! *reveal-next-frame?* #f)
      (commit!
        (model.focus-frame-observed *model* snapshot geometry))]
    [*reveal-next-frame?*
      (set! *reveal-next-frame?* #f)
      (commit! (model.reveal-frame-observed *model* geometry))]
    [else (commit! (model.geometry-observed *model* geometry))])
  (define model-at-render *model*)
  (define current-layout (model.presented-layout model-at-render))
  (if current-layout
    (begin
      (component.apply-clip! (layout.width current-layout))
      (render.draw!
        frame
        current-layout
        (model.row-facts model-at-render)
        (theme.resolve
          *theme-sources*
          (model.icons? model-at-render)
          (model.guides? model-at-render)))
      (set! *latest-frame*
        (rendered-frame (model.root model-at-render) current-layout)))
    (release-pane!))
  frame)

(define (handle-event! event)
  (define current-layout
    (and
      *latest-frame*
      (equal? (rendered-frame-root *latest-frame*) (model.root *model*))
      (rendered-frame-layout *latest-frame*)))
  (unless current-layout
    (input.cancel!))
  (define result (input.handle! *model* current-layout event))
  (define update-result (input.result-update result))
  (when update-result
    (commit-and-redraw-if-needed! update-result))
  (input.result-pass-through? result))

(define (environment-text name)
  (with-handler (lambda (_cause) #f) (env-var name)))

(define (joined left right)
  (cond
    [(not (string? left)) right]
    [(not (string? right)) left]
    [else (string-append left (string-append ":" right))]))

; The environment is read once. `EZA_COLORS` follows `LS_COLORS` so its rules
; win, which is the order `eza` itself resolves them in. A missing variable
; leaves Grove on its Theme roles rather than failing startup.
(define (ls-colors-text source)
  (cond
    [(equal? source 'environment)
      (joined
        (environment-text "LS_COLORS")
        (environment-text "EZA_COLORS"))]
    [(string? source) source]
    [else #f]))

(define (entry-palette-for source)
  (define text (ls-colors-text source))
  (and
    (string? text)
    (> (string-length (trim text)) 0)
    (palette.parse text)))

(define (start-runtime! side width icons? guides? visibility ls-colors)
  (set! *model*
    (model.init
      side
      width
      icons?
      guides?
      visibility
      (entry-palette-for ls-colors)))
  (component.install! side render-current! handle-event!)
  (hooks.install! dispatch!)
  (set! *reveal-next-frame?* #t)
  (subscribe-to-refresh!)
  (reveal-now!))

(define (start! side width icons? guides? visibility theme-sources ls-colors)
  (when *started?*
    (error "Grove has already started"))
  (set! *started?* #t)
  (set! *theme-sources* theme-sources)
  (enqueue-thread-local-callback
    (lambda ()
      (start-runtime! side width icons? guides? visibility ls-colors)))
  #t)

(define (enqueue-after-start! action)
  (when *started?*
    (enqueue-thread-local-callback action))
  void)

(define (focus!)
  (enqueue-after-start!
    (lambda ()
      (set! *focus-next-frame?* #t)
      (helix.redraw))))

(define (visibility-toggle!)
  (enqueue-after-start!
    (lambda ()
      ; Keep this struct-to-struct conversion direct. ADR 0001 covers Steel
      ; JIT corruption.
      (commit-and-redraw-if-needed!
        (model.visibility-toggle-requested *model*)))))
